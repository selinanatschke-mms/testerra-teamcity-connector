plugins {
    id "io.codearte.nexus-staging" version "0.30.0"
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

ext {
    // Minimum required Testerra version
    testerraCompileVersion = '2.0'
    // Unit tests use the latest Testerra version
    testerraTestVersion = '[2,3-SNAPSHOT)'
    moduleVersion = '2-SNAPSHOT'
    if (System.properties.containsKey('moduleVersion')) {
        moduleVersion = System.getProperty('moduleVersion')
    }

    group 'io.testerra'
    version moduleVersion
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

// Do not move this integration because `group` and `version` is needed for publishing
apply from: rootProject.file('publish.gradle')

dependencies {
    compileOnly 'io.testerra:driver-ui-desktop:' + testerraCompileVersion

    testImplementation 'io.testerra:driver-ui-desktop:' + testerraTestVersion
    testImplementation 'io.testerra:report-ng:' + testerraTestVersion
    // Capture System.out.println
    testImplementation 'io.github.hakky54:consolecaptor:1.0.2'
}

test {
    useTestNG() {
        suites file('src/test/resources/suite.xml')
    }

    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }

    options {
        systemProperties(System.getProperties())
    }

    // basically execution returns "GREEN" (framework exits with exit code > 0 if there were failures)
    ignoreFailures = true
}
